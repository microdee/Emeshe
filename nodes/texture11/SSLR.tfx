
#include "../fxh/MRE.fxh"
#include "../fxh/Materials.fxh"

Texture2D ColorTex : PREVIOUS;

float2 R : TARGETSIZE;

cbuffer controls:register(b0){
	float StepCount = 16;
	float FineStepCount = 16;
	float rayLength = 10;
	float EpsilonMul = 1;
	float uvshapepow = 4;
	float uvfadepow = 4;
	float distpow = 1;
	float distclamp = 0;
};

#if !defined(DISCRAYS)
    #define DISCRAYS 6
#endif


SamplerState s0 <string uiname="Sampler";>
{
    Filter=MIN_MAG_MIP_LINEAR;
    AddressU=Border;
    AddressV=Border;
	BorderColor=float4(0,0,0,0);
};

float2 GetUV (float3 p)
{
	float4 trpos = mul(float4(p, 1), CamProj);
	float2 uv = trpos.xy / trpos.w;
	uv.y *= -1;
	return (uv+1) * 0.5;
}

struct RayData
{
	float3 vpos;
	float2 uv;
	float dist;
};

RayData RayCast(float3 OPos, float3 Dir, float RayL, float EpsMul)
{
	float cStepL = RayL/StepCount;
	float3 raypos = OPos;
	bool hit = false;
	float4 col = 0;
	
	RayData o = (RayData)0;
	
	for(float i=0; i<StepCount; i++)
	{
		raypos += Dir * cStepL;
		float2 cuv = GetUV(raypos);
		float3 cnorm = Normals.SampleLevel(s0, cuv, 0).xyz;
		float3 cpos = GetViewPos(s0, cuv);
		if(dot(cnorm, Dir) < 0)
		{
			float cdist = distance(cpos, raypos);
			if(cdist < cStepL)
			{
				raypos -= Dir * cStepL * 0.5;
				float fStepL = cStepL / FineStepCount;
				for(float i=0; i<StepCount; i++)
				{
					raypos += Dir * fStepL;
					float2 fuv = GetUV(raypos);
					float3 fnorm = Normals.SampleLevel(s0, fuv, 0).xyz;
					float3 fpos = GetViewPos(s0, fuv);
					if(dot(fnorm, Dir) < 0)
					{
						float fdist = distance(fpos, raypos);
						if(fdist < fStepL * EpsMul)
						{
							hit = true;
							o.vpos = raypos;
							o.uv = fuv;
							o.dist = distance(OPos, raypos);
							
							break;
						}
					}
				}
			}
			if(hit) break;
		}
	}
	return o;
}

float4 SSLR(float4 PosWVP:SV_POSITION,float2 uv:TEXCOORD0): SV_Target
{
	//float stencil = GetStencil(R, uv);
	//uint matid = GetMatID(s0, uv);
	float4 col = float4(0,0,0,1);

	float3 vPos = GetViewPos(s0, uv);
	float3 onorm = Normals.Sample(s0,uv).xyz;
	float3 rrV = normalize(vPos);
	float3 R = reflect(rrV, onorm);

    RayData oray = RayCast(vPos, R, rayLength, EpsilonMul);
	
	float uvd = pow(pow(abs(oray.uv.x-.5), uvshapepow) + pow(abs(oray.uv.y-.5), uvshapepow), 1/uvshapepow);
	float fadeuv = saturate(1 - pow(uvd*2,uvfadepow));
	float fadedist = 1 - oray.dist/rayLength;
	
	float distout = pow(lerp(saturate((oray.dist-distclamp)/rayLength),1,oray.dist==0), distpow) * rayLength;
	
	float4 colout = ColorTex.SampleLevel(s0, oray.uv, 0);
	colout.rgb *= fadeuv * fadedist;
	colout.a = distout;
	
    return colout;
}

technique11 sslr{
    pass P1
    {SetPixelShader(CompileShader(ps_5_0,SSLR()));}
}